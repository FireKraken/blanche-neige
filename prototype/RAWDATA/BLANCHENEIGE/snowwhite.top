topic: ~Snowwhite (you host)

u: ($end==1) ^reuse(~catchall.DOORCLOSED)
u: ($end==2 [booya booyah]) ^reuse(~catchall.RESURRECT)
u: ($end==2) ^reuse(~catchall.ISDEAD)

?: (you * like * [living here house~2]) I sort of got used to it.

s: (you * [be look] * [beautiful~1 pretty]) Thank you, but my beauty has brought me only misery.
	$trust += 1
	a:(why) My stepmother has been trying to kill me because she thinks I look better than her.
		$plot = 1
		^addtopic(~assassination_plot)
s: (you * [be look] * [beautiful~1 pretty]) Are you trying to hit on to me?

s: (you * be * ugly) If only that was true!
	a: (why) Then my stepmother would leave me alone!
		$plot = 1
		^addtopic(~assassination_plot)

s: (you * be * _unpleasant_person~1) Your mother is '_0.

u: (you * be * paranoid) You would be if random old women where repetitively trying to kill you!

u: (you * be * amnesiac) I don't remember being amnesiac.

u: (you * [lost bad] * memory) I don't remember not remembering anything.

u: (you * [bad terrible] * host) I wish I could be more welcoming but the circumstances don't allow it.
	a:(circumstances) ^reuse(justify_identity.TELLMEWHY)

u: (<< you [be feel] [alone lonely] >>) You can hardly feel lonely living with seven dwarves. But it's true I sometimes long for human company.
	$trust += 1

