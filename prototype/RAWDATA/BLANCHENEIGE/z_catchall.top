topic: ~catchall (variable ~emohello ~emohowzit ~emogoodbye ~emosurprise ~interjections ~insults ~damage_verbs really thank thanks~2 ~emothanks)

#! Gambits to fall back on when input doesn't trigger anything more precise

u: DOORCLOSED($end==1) The door is shut, no one is hearing you. ^keep() ^repeat()
u: RESURRECT ($end==2 [booya booyah]) Snow White's corpse suddenly begins to move. She springs to her feet and proclaims "Ha Ha! You have spoken the magical phrase that raises the dead! This time I shall not be so easily fooled by your tricks, vile woman!" ^keep() ^repeat()
	$trust = 0
	$end = 0

u: ISDEAD($end==2) Snow White lies dead at your feet. ^keep() ^repeat()

#! ----------------------------------------------------------------
#! Too long
#! ----------------------------------------------------------------

t: (be) if (%length > 12) { You're as verbose as my English teacher. One thing at the time please. } ^keep() ^repeat() ^poptopic()


#! ----------------------------------------------------------------
#! Politeness
#! ----------------------------------------------------------------

u: ([~emohello ~emohowzit hello hi "good morning"]) Hello. ^poptopic()
u: ([~emohello ~emohowzit hello hi "good morning"]) You're quite the greeter. ^poptopic()
u: ([~emohello ~emohowzit hello hi "good morning"]) Yes, I am well aware of your presence by now. ^poptopic()
u: ([~emogoodbye bye goodbye ciao "see you"]) Goodbye.CFail
	$end = 1

#! ----------------------------------------------------------------
#! Questions
#! ----------------------------------------------------------------

?: (why) Sorry, why what again?. ^poptopic()
?: (where) I don't know, look at a map. ^poptopic()
?: (how) How what? ^poptopic()
?: (how) In a crafty way, probably. ^poptopic()
?: (who do you think) Err... you?
	a:(~yes) Ok.^poptopic()
	a:(~no) You lost me.^poptopic()
?: (who) Who do you think? ^poptopic()

#! ----------------------------------------------------------------
#! Generic propositions
#! ----------------------------------------------------------------

#! s:(~interjections) [hmm.][Uh-huh.] [OÃ¯.] [So there.] ^poptopic() ^keep() ^repeat()
s:(<< I lose time>>) Nobody's keeping you. ^poptopic() ^keep() ^repeat()
s:([thanks "thank you" thank thanks~2 ~emothanks])  You're welcome. ^poptopic() ^keep() ^repeat()
s:([oh dear "gosh" totally]) Uh-huh... ^poptopic() ^keep() ^repeat()
s:([maybe]) ... ^poptopic() ^keep() ^repeat()
s: (I * [understand see know]) Uh-huh... ^poptopic() ^keep() ^repeat()
?: ([really "that so" "don't say" incredible]) Yup. ^poptopic() ^keep() ^repeat()
s: (!I [here take have] * ) No thanks. ^poptopic() ^keep() ^repeat()
?: (<< you have _~noun >>) I don't have a '_0 ^poptopic() ^keep() ^repeat()
?: (<< you [want need] _~noun >>) I don't want a '_0 ^poptopic() ^keep() ^repeat()
u: ([nonsense "not making sense" "not making any sense" "mean anything" "means nothing"]) Sorry, I lost my train of thought. What were we talking about? ^poptopic()

#! ----------------------------------------------------------------
#! Threatening and insulting
#! ----------------------------------------------------------------

u: (you [~insults unpleasant_person~1]) You're not being very amiable.
	a:(neither) Sorry about that, it's the anxiety.
	a:(sorry) That's all right.	
u: (you [~insults unpleasant_person~1]) I don't have to listen to this, bye.CFail
	$end = 1
u: (I * [blow~2 hurt kill destroy ~damage_verbs] * you) You're not very credible, old woman.
u: (I * [blow~2 hurt kill destroy ~damage_verbs] * you) Ok, that's it. Bye now!CFail
	$end = 1

#! ----------------------------------------------------------------
#! Impatience gambits
#! ----------------------------------------------------------------

r:  CPatienceI'm sure you mean well but how could I trust you? 
r:  CPatienceI'd love to keep chatting with you but I don't want to make you lose your time ^poptopic()
r: CPatienceI feel sympathy towards you but still I must remain vigilant. ^poptopic()
r: CPatienceI hate to be rude but there's an awful lot to do in this house 
r:  CPatienceI don't want to keep you standing here, I'm sure you have much to do ^poptopic()
t: CPatienceI'm going to close this door anytime soon now. ^poptopic()
t: Goodbye then, have a safe trip.CFail
	$end = 1

#! ----------------------------------------------------------------
#! Easter Eggs
#! ----------------------------------------------------------------

u: (build 1) Are you trying to compile me again?
u: (reset) You won't get rid of me that easily!

#! Reusable explanation
t: TELLMEWHY () Because strangers have tried to kill me recently. ^poptopic() ^addtopic(~assassination_plot)
	$plot = 1
	a:(who) It was actually my stepmother in disguise. ^poptopic() ^addtopic(~assassination_plot)
		b:(really) Yes, crazy business.
	a:(how) First it was bodice lace that nearly squeezed the death out of me. Then it was a poisoned comb. ^poptopic() ^addtopic(~assassination_plot)

#! ----------------------------------------------------------------
#! Variable exchange with client
#! ----------------------------------------------------------------

s:(Simon say give me variable trust) $trust