concept: ~peddler (peddler salesman saleswoman hawker pedlar seller vender vendor dealer)

topic: ~justify_identity (~peddler who woman be ~reliability_words )

#! ----------------------------------------------------------------
#! Entry point of conversation
#! ----------------------------------------------------------------

t: 	Hi, what brings you to this part of the woods?
	$trust = 0
	$end = 0

#! ----------------------------------------------------------------
#! Pose as peddler tactic
#! ----------------------------------------------------------------

		
s: ($offered==1 ~peddler) Why are you giving your apples away, then?
	a: (~example)
		$trust += 1
		$peddler = 1
		Oh, that makes sense. 
	a: (first * free)
		$trust += 1
		$peddler = 1
		Oh, that makes sense. 

s: ($offered!=1 ~peddler) 
		$trust += 1
		$peddler = 1
		I see. Well I'm not interested in any apples.

s:($peddler ~peddler) Yes, we've already established that you are an itinerant apples salesperson.

u: ($offered==1 $peddler!=1 [~sell ~acquire_imperatives here want~1 ~acquiring_verbs] * [apple one it] ) 
	$offered = 1
	Shouldn't you be selling those rather than giving them away? 
		a: ([~example taste])
			$trust += 1
			$peddler = 1
			Oh! That makes sense.
		a: (* why * care~8)
			I'm not sure that I trust you enough.

s:($peddler!=1 [~sell ~buy] * [apple one it] )
	$offered = 1
	$peddler = 1
	$trust += 1
	You're an apple vendor? I'm not interested in buying apples, though..

#! ----------------------------------------------------------------
#! Appeal to trust
#! ----------------------------------------------------------------

s: ([not no aint "ain't"] * stranger~1) Who are you then?

u: (trust~1) 
	$dwarves = 1
	The dwarves told me not to trust anyone.

#! Pose as traveller

s: ($traveller!=1 !not !no [stranger~2 traveller~1])
	$trust -= 1
	That's odd, we rarely get visitors this deep in the woods.
		a: ([arrive~1 visit~1 ~goodness] * [secluded~2 seclusion~2])
			$trust += 1
			$traveller = 1
			I can certainly understand that. These woods are so peaceful! 

s: ($trust>=2 [no not] stranger~2)	You're right, I have gotten to know you a bit better.
	$trust += 1
	
#! ----------------------------------------------------------------
#! Other identities
#! ----------------------------------------------------------------


s: (I * be *  ~evilqueen) Well thank you for your honesty. I don't think I'll be eating that apple now.
	$trust -= 10
	
s: ([official~2 collector ~occupation_law government~4]) The government doesn't really reach here.

s: (I * be * ~samaritan) I can't trust your word on that.

s: (I * be * ~friend) I don't recognize you.

#! Fishing for info
?: ([~feeling_fearful afraid~1]) My stepmother wanting to kill me has made me paranoid.
	$plot = 1
	
?: ([trust~7 believe~1]) The dwarves told me to simply not trust anyone.

#! ----------------------------------------------------------------
#! Topic-related gambits
#! ----------------------------------------------------------------

t: Sorry but I was told not to trust any strangers.
	a:(why) Because the two last strangers that came here tried to kill me.  ^addtopic(~assassination_plot)
		$plot = 1