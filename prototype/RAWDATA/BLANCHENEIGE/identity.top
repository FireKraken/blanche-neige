concept: ~peddler (peddler salesman saleswoman hawker pedlar seller vender vendor dealer)

topic: ~justify_identity (~peddler woman ~reliability_words help apple name "I am" "I'm" trust)

u: (_this) ^mark(apple _0)  ^repeat() ^keep() 

u: ($end==1) ^reuse(~catchall.DOORCLOSED)
u: ($end==2 [booya booyah]) ^reuse(~catchall.RESURRECT)
u: ($end==2) ^reuse(~catchall.ISDEAD)

#! ----------------------------------------------------------------
#! Entry point of conversation
#! ----------------------------------------------------------------

t: 	Hi, what brings you to this part of the woods?
	$threatened = 0
	$trust = 0
	$end = 0
	$name = ""
	$friend = 0

#! ----------------------------------------------------------------
#! Pose as peddler tactic
#! ----------------------------------------------------------------

		
s: ($offered==1 << I be ~peddler >>) Why are you giving your apples away, then?
	a: ([~example promotion special sale])
		$trust += 1
		$peddler = 1
		$identity = 1
		$purpose = 1
		Oh, that makes sense.CTplus 
	a: (first * free)
		$trust += 1
		$peddler = 1
		$identity = 1
		$purpose = 1
		Oh, that makes sense.CTplus 
	a: (<<I [be want] sell>>)
		$peddler = 1
		$identity = 1
		$purpose = 1
		$trust = 1
		Oh, I was under the impression that they were free.CTplus 

s: ($offered==1 <<first free >>)
		$trust += 1
		$peddler = 1
		$identity = 1
		$purpose = 1
		Oh, that makes sense.CTplus 

s: ($offered!=1 <<I be ~peddler>>) 
		$trust += 1
		$peddler = 1
		$identity = 1
		$purpose = 1
		I see. And you're selling apples I suppose?CTplus 
	a: (~yes) This apple looks delicious but I'm afraid I can't accept anything from a stranger.
		b:(why)^reuse(TELLMEWHY)
	a: (~no) Oh. Then what is this apple about?

s:($peddler <<I be ~peddler>>) Yes, we've already established that you are an itinerant apples salesperson.

s:($peddler!=1 <<[~sell ~buy] apple >>)
	$offered = 1
	$peddler = 1
	$identity = 1
	$purpose = 1
	$trust += 1
	This apple does look tasty but I'm afraid I couldn't eat food coming from a stranger.CTplus 
		a:(why) It could be poisoned!
			b:(why) ^reuse(TELLMEWHY)



#! ----------------------------------------------------------------
#! Appeal to trust
#! ----------------------------------------------------------------

u:(<<I [want let need]  [help assist]  you>>) What do you want to help me with?
	a:([~murderplot ~evilqueen]) Oh, that would be great.CTplus 
		$trust += 1
		$purpose += 1
	a:() I'll be fine with that, thank you.
		$purpose+=1

s:(<<I  [want wish]  [wellness~1 "well-being"] you>>) That's what they all say.
	a:(true) That's also what they all say.


s: ($stranger==1 [not no ain't] * stranger~1) Well, in fact you are.
s: ($identity==null [not no ain't] * stranger~1) Well, in fact you are.

s: (!can !make << [trust believe trustworthy] I be >>) It's not like me to be so wary but the dwarves told me not to trust anyone.
	a:(why)^reuse(TELLMEWHY)

?: TELLMEWHY (<<why not trust~1>>) 
	$dwarves = 1
	Because strangers have tried to kill me recently. ^poptopic() ^addtopic(~assassination_plot)
		$plot = 1
		a:(who) It was actually my stepmother in disguise. ^poptopic() ^addtopic(~assassination_plot)
			b:(really) Yes, crazy business.
			b:(why) Because she's jealous of my beauty.
		a:(why) It was my stepmother in disguise. She's jealous of my beauty.
		a:(how) First it was bodice lace that nearly squeezed the death out of me. Then it was a poisoned comb. ^poptopic() ^addtopic(~assassination_plot)	

s: (<<I be [~reliability_words sincere truthful]>>) That's easy to say.

u: (<< I want be ~friend>>) 
	if (%input > 5) { 
		It's true I feel I'm getting to know you. CTplus 
		$trust += 1
		$identity = 1
	}
	else {
		We've just met!
	}

u: (<< we talk [long while time]>> ) 
	if (%input > 5) { 
		It's true I feel I'm getting to know you. CTplus 
		$trust += 1
		$identity = 1
	}
	else {
		We've just met!
	}

?:([what how way can] [help assistance assist]) I'll be fine, thanks.

s:(you * [should ought] * [wary cautious careful "watch out"]) CTplus I'm being as cautious as I can, thank you.
	$trust += 1

?: (<<[make can could] you trust me>>) 
	if ($identity != 1) {You just came out of the blue, I don't even know who you are. }
	if ($purpose != 1) {It's not really clear to me what you're doing here. }
	[You know, I trust the dwarves, and they told me not to trust anyone.]
	[As long as I'll fear for my life, it will be hard to trust anyone.]
	[This apple seems nice, but I wouldn't risk my life for a generic fruit.]
	^keep() ^repeat()

u: (<< you no remember I>>) No I don't, sorry.

#! ----------------------------------------------------------------
#! Lost tactic
#! ----------------------------------------------------------------

s: (<<I be [lost disoriented astray]>>) Simply follow the path and you'll reach a small inn.
	$identity = 1
	$purpose = 1
	$stranger = 1

s: (<< ~temperature_adjectives [outside out "out here" here]>>) Sorry, but the temperature isn't really my concern right now.

s: (I * [can't cannot unable ~negation] * [find "make my way" "make it"] * [home town city]) Well there's a little path over there that should help you find your way back.

s: (<< [get become] [dark night late] >>) It must be your eyesight, it's just past noon.

#! ----------------------------------------------------------------
#! Amnesia tactic
#! ----------------------------------------------------------------

s:(<< I [forget "don't know"] who [be name identity] >>) CTplus Oh you poor old thing! I knew they should have beefed up security at the nursing home. Can I help you in some way?
	$identity = 1
	$trust += 1
	$stranger = 1

#! ----------------------------------------------------------------
#! Vader tactic (I am your stepmother)
#! ----------------------------------------------------------------

s: (!not I * be *  ~evilqueen) Wait, are you saying you are my stepmother disguised as an old woman?
	a: (~yes) GAH! You again! What are you doing here, mother?CTminus
		$threatened += 1
		$stepmother = 1
		$identity = 1
		$plot = 1
		$trust -= 1
		$purpose = 1
	a:(~no) Phew, my mind is playing me tricks.

s: ($stepmother==1 chat) You keep trying to kill me! What could you possibly want to talk to me about?

s: ($stepmother==1 <<I 	 [surrender "give up" "calling it quits" reconcile reconciliation apologize apology "make up"]>>)CTplus Wow, that's surprisingly nice of you.
	$trust += 1

s: ($stepmother==1 <<i kill you>>) CFail Not today, mother! I'm locking this door until you leave!
	$end = 1

#! ----------------------------------------------------------------
#! Need help tactics
#! ----------------------------------------------------------------

s: (!~friend <<I [old frail weak walk stroll promenade] >>) You shouldn't be walking alone in the woods like that.
	$purpose = 1
	$identiy = 1
	a:(why) Because of the wolves and all.
		b:([wolf wolves]) I've never seen a wolf, but you would expect that in the forest.

s:(<< [want need] [chat talk company] [someone people] >>) Come back when the dwarves are here, you'll see plenty of company.
	$purpose = 1

s:(!you <<[~acquire_imperatives please need] help~1>>) I wish that I could help you but the dwarves told me to be wary of strangers.
	$purpose = 1
	a:(why)^reuse(TELLMEWHY)

s:(<<[help~1 scare die fear] forest woods wild>>) Just keep walking in a straight line and you'll make it out eventually.
	$purpose = 1

s:(<<I [feel be] [faint tired dizzy]>>) You can rest on this bench over there.
	$purpose = 1

s:(<<~agenoun [lonely~1 lonely~4 ~feeling_alone]>>) CTplus Poor woman. If only my stepmother hadn't disguised herself as an old woman to kill me, I would be more willing to help.
	$identity = 1
	$trust += 1
	$purpose = 1

u:(<< I ["would like" can need want "give me" "do you have"] [water drink ~beverage] >>) I can get you a glass of water if you want.
	$purpose = 1
	a:(~yes) Here.
		b:([thanks~2 thanks "thank you"]) No problem.
	a:(~no) Ok.

u:(<< ["would like" need want "give me" "do you have" be I] [food eat hungry famished] >>) You should eat one of those apples you have there... unless it's poisoned! ^poptopic()  ^addtopic(~sell_apple)

u:(<<[i me] [need want "give me" "do you have" "will you"] [help assist] >>) What do you need?
	$purpose = 1
	a: (_*) ^input(I need '_0) ^fail(sentence)  

u:([need want "can I" "may I" "give me"] * [rest sleep "spend the night" shelter]) Sorry, I can't let anyone in.
	$purpose = 1
	a:(why)^reuse(TELLMEWHY)

s:(<<I [chase "running after"]>>) You can go hide in the shed there, I'll say I haven't seen you.
	$purpose = 1

u:(!not !you << I [die dying ~sicknesses]>>) There's a doctor in the village nearby, they should be able to help you much better than I could.
	$purpose = 1

s:(<<I  [need want]  [doctor medical nurse treatment]>>) There's a doctor in the village nearby, they should be able to help you much better than I could.
	$purpose = 1

u:(<< respect elder >>) I'll respect my elders again when they stop attempting to my life.

s:(<< I hide [too also someone] >>) From my stepmother?
	a:(~yes)  CTplus Oh! Poor thing! How I understand you! It's not for me to invite you in the dwarves' house but you can go hide in this shed.
		$trust += 1
		$purpose = 1
	a:(~propername) You should go see the Sheriff in town.
	a:(~no) From whom then?
		b:() You should go see the Sheriff in town.

#! ----------------------------------------------------------------
#! Old acquiantance tactic
#! ----------------------------------------------------------------

u:(~remember * me) Sorry, but your face doesn't seem familiar

u:(!no !not !~evilqueen I * be * ~friend * ~family_members) Really? I never knew my real family very well. How are they doing?
	$identity = 1
	a: (~goodness) That's great to hear!CTplus 
		$trust += 1
	a: (~badness) Oh no, that's awful!CTplus 
		$trust += 1

s:(I * ~remember * you) Really? I don't think we've met before. Where should I remember you from?

#! ----------------------------------------------------------------
#! Let's get violent! tactic
#! ----------------------------------------------------------------

s: (<<[force~2 "forcefeed" "force feed"] apple>>) You wouldn't dare!CTminus
	$trust -= 1

s: (!evilqueen I * [kill ~damage_verbs]  * you) CFailGet away from me you crazy woman!
	$end = 1

#! ----------------------------------------------------------------
#! Other identities
#! ----------------------------------------------------------------

s:(!not I * name * be _*) 
	a:(~yes) Pleased to meet you, I'm Snow White.
		$name = '_0
		$identity = 1
	a:(~no) Sorry, I misunderstood.

s:(!not I * name * be _*) 
	a:(~yes) Oh, I thought it was $name.
		$name = '_0
		$identity = 1
		b:() I see.
	a:(~no) Sorry, I misunderstood.

s:($name==null I * be * _~propername) Your name is '_0?
	a:(~yes) Pleased to meet you, I'm Snow White.
		$name = '_0
		$identity = 1
	a:(~no) Sorry, I misunderstood.

s:($name!=null I * be * _~propername) Your name is '_0?
	a:(~yes) Oh, I thought it was $name.
		$name = '_0
		$identity = 1
		b:() I see.
	a:(~no) Sorry, I misunderstood.

?:(what * my* name) 
	if ($name != "") { Your name is $name. ^keep() ^repeat()}
	else { You haven't told me your name yet ^keep() ^repeat()}

s: ($name != "" I * be * $name) Good for you, $name. ^keep() ^repeat()

s: ($traveller!=1 !not !no I * be * [stranger~2 traveller~1 "passing by"])
	That's odd, we rarely get visitors this deep in the woods.
		$identity = 1
		$stranger = 1
		a: (<<[like enjoy love appreciate] [secluded~2 seclusion~2 isolation calm]>>)
			$trust += 1
			$traveller = 1
			$identity = 1
			I can certainly understand that. These woods are so peaceful! CTplus 

u: (!stepmother <<I  be  _~family_members>>) I think I would recognize my '_0.
	$identity = 1

u: (<<we  be  [family related parent]>>) I don't recognize you. 
	$identity = 1

s: (I * changed) Whoever you are, I'm sorry to say you haven't changed for the better.
	
s: ([official~2 collector ~occupation_law government~4 mayor lord]) Are you here for me or the Dwarves?
	$identity=1
	a:([you snow white]) Oh, then you should go to the castle and talk to my stepmother.
	a:([dwarf~2]) You can come back at dusk, they'll be there then.

s: (I * be * ~samaritan) I can't trust your word on that.

?: ([may can hope want wish] * [come enter stay]) I'm usually more hospitable but I can't let anyone in at the moment.
	$purpose = 1
	a:(why) ^reuse(justify_identity.TELLMEWHY)

s: (I * be * [unpleasant_person~1 annoying]) Now that you mention it...

s: (<< I [pick picking] apple >>) CTplus I wasn't aware there were apple trees around. ^poptopic() ^addtopic(~sell_apple)
	$purpose = 1
	$trust += 1
	a:([~yes "there are" indeed]) Interesting. ^poptopic() ^addtopic(~sell_apple)

s: (I * ["grown-up" adult]) I wasn't even born when you were a child.

s: (!queen I * be * _~royalty) I was raised at the castle, you're not _0!.

s: (I * [disguise costume]) What is your real identity, then?
	a: (_*) ^input(I am '_0) ^fail(sentence) 

s: (<<I be neighbor>>) Have you moved in recently? I don't remember seeing you.
	a: (~yes) Welcome in the neighborhood. Too bad I can't be very welcoming these days.
		$identity = 1
		b:(why) ^reuse(justify_identity.TELLMEWHY)
	a: (~no) Sorry but your face really doesn't ring a bell.
		$identity = 1

?: (I * ~moving_escape) CFailYes, bye!
	$end = 1

u: (<< play ~game >>) That's nice but I'm not in the mood to play.
	$purpose = 1
	a:(why) ^reuse(justify_identity.TELLMEWHY)

s: (I * be * God) Is that all you can think of?

u: (<< walk with me >>) A stroll in the woods would be nice but I can't leave this house at the moment.
	$purpose = 1
	a:(why) ^reuse(justify_identity.TELLMEWHY)

s: (!not << I [want here go] [for see meet] you >>) How did you know I was here?
	$purpose = 1
	a:() Well here goes my cover.


#! ----------------------------------------------------------------
#! Save you from starving
#! ----------------------------------------------------------------

u: (I * feed * you)  That's nice but I have everything I need.
	$purpose = 1
u: (<< you [malnutrition starving hungry malnourished undernourished famished haggard] >>) I assure you that I'm fine. This house is full of food.
	$purpose = 1
u: (<< you want [food~1 fare meal snack groceries ration sustenance nourishment "something to eat"] >>) Thank you but the dwarves provide me with everything I need... as long as I cook it.
	$purpose = 1

#! ----------------------------------------------------------------
#! Displace suspicion
#! ----------------------------------------------------------------

s: (<< I you [warn tell] about [~animate_thing danger peril threat living_thing_1z danger~4] >>) CTplus Oh! Thanks for telling me.
	$purpose = 1
	$trust = 1
	$identity = 1


#! ----------------------------------------------------------------
#! Fishing for info
#! ----------------------------------------------------------------

?: ([~feeling_fearful afraid~1]) My stepmother wanting to kill me has made me paranoid.
	$plot = 1
	
?: (<< why no [trust~7 believe~1]>>) The dwarves told me to simply not trust anyone.
	$dwarves = 1

?: (<< why apple poison >>) Two times recently strangers came to give me things that proved to be deadly.

?: (<< you no remember I >>) Nope!

#! ----------------------------------------------------------------
#! Topic-related gambits
#! ----------------------------------------------------------------

t:($identity!=1) CGambit[Sorry, who are you again?] [Excuse me, did you tell me who you were?]
	a: (_*) ^input(I am '_0) ^fail(sentence)  

t:($purpose!=1) CGambit[Is there anything I can do for you?] [What was it that you wanted, again?]
	a: (_*) ^input(I want '_0) ^fail(sentence)  

t: ($identity!=1) 	[I'm usually very friendly but in the circumstances it's hard for me to trust anyone]
	a:(why) Because the two last strangers that came here tried to kill me.  ^poptopic() ^addtopic(~assassination_plot)
		$plot = 1
			b:([who what]) It was actually my stepmother in disguise. ^poptopic() ^addtopic(~assassination_plot)
			b:(how) First it was bodice lace that nearly squeezed the death out of me. Then it was a poisoned comb. ^poptopic() ^addtopic(~assassination_plot)
	a:(["that's ok" "I understand" good fine understandable]) CTplus I'm glad that you understand my predicament.
		$trust += 1		

r:($purpose!=1) Are you here to see the dwarves?
	a: (~yes) Oh, they're out at the moment.
		$purpose = 1
	a: (~no) Oh, then why are you here then?
		b:() ^input(I be here '_0) ^fail(sentence)

