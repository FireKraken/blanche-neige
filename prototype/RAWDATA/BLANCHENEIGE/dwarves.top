concept: ~insult (suck blow "lame" loser losers)

topic: ~dwarves (dwarf~2 ~mythical_intelligent_being dwarves house cook)

#! Possible related pronouns
u: (_they) ^mark(dwarf _0)  ^repeat() ^keep() 
u: (_them) ^mark(dwarf _0)  ^repeat() ^keep() 
u: (_their) ^mark(dwarf _0)  ^repeat() ^keep() 

u: ($end==1) ^reuse(~catchall.DOORCLOSED)
u: ($end==2 [booya booyah]) ^reuse(~catchall.RESURRECT)
u: ($end==2) ^reuse(~catchall.ISDEAD)

#! ----------------------------------------------------------------
#! Undermine dwarves tactic
#! ----------------------------------------------------------------

u: ([they dwarf~2] * [keep prison~1 ~jail kidnap~1 confine~2 trap hostage]) 
	You think so? ... they seem so nice.
	$trust += 1

u: (<< trust dwarf >>) I don't know where I would be if it weren't for the dwarves' help.

u: (<< you [cook clean chores work~1z ~household_tasks servant slave dishes] dwarves >>) It's true they treat me a bit like their servant.
	$trust += 1

?: (<<you [keep prison~1 ~jail kidnap~1 confine~2 trap hostage]>>) No, I'm free to leave if I want to.

u: ([they dwarf~2] * stupid~1)
	$trust += 1
	Yeah, I guess they are a little bit silly sometimes. Can you believe that they thought the earth was round?
	a:([yes "it is" "earth is round"]) Ha ha.

s: ([they dwarf~2] * [unpleasant_person~1 ~insult])
	$trust -= 1
	Don't talk like that about these generous creatures!


u: ( << you [dwarf~2] [food~1 apple] [give feed] >>)
	We mostly eat cabbages. I admit I'm a bit tired of that.
	$trust += 1
		a: (<<try apple>>) I guess it could make for a nice change.
		$trust += 1


u: (<< [normal natural suitable fitting moral think virtue virtuous proper] live with dwarf~2>>) Is it wrong for a young girl to live with seven grown--albeit small--men?
	a:(no) Oh dear, I had never thought of this that way.
		$trust += 1
	a:(yes) It does feel right.
		$trust += 1

#! ----------------------------------------------------------------
#! Praise dwarves tactic
#! ----------------------------------------------------------------

s: (<< dwarf~2 be [wise clever ~good_intelligence_adjectives] >>) Yes, they're nobody's fools.
	$trust += 1

s: (<< dwarf~2 be [nice good affable amiable compassionate considerate cool pleasant sweet likable charming fun lovely ] >>) They've been very good to me.
	$trust += 1

#! ----------------------------------------------------------------
#! Relation of the dwarves tactic
#! ----------------------------------------------------------------

s: ( << friend~1 [dwarf~2 they] I >>) They never mentioned you.
	$identity=1

u: ( << I dwarf~2 [meet see appointment rendez-vous come] >>) You can come back when they're there.
	a:(when) At dusk.

s: (<< dwarf~2 [I you] message>>) What's the message?
	a:() That doesn't sound like them. Their messages are always in rhyme.

?: (<< dwarf~2 be [home house inside here available] >>) This is their house but they're out at the mountain.
	$dwarves = 1

s: (<< apple dwarf~2 [bring deliver sell for] >>) The dwarves didn't tell me about you, come back when they're there.
	a:(when) At dusk.

u: (<< dwarf~2 I ~role_romantic >> ) Really?! They never told me about that! If that's true they'll need to introduce you themselves.
	$trust += 1

#! ----------------------------------------------------------------
#! Dwarven approval
#! ----------------------------------------------------------------

s:(dwarf * [apple] * [ok good accept acceptable "good for you"]) Well maybe if the dwarves said so...
	$trust += 1

#! ----------------------------------------------------------------
#! Other Tactics
#! ----------------------------------------------------------------

u: (<<[they dwarf~2] [no not won't "will not"] [know "find out"]>>) The problem isn't whether or not they know. It's a question of my own safety!

u: (<< you come out >>) I don't have time for that with all the dishes waiting.

#! ----------------------------------------------------------------
#! Fishing for info
#! ----------------------------------------------------------------

?: (who * be * dwarf~2) I live with seven little men.
	$dwarves=1
	a:(why) They've been very nice to me.

?: (where * be * dwarf~2) Digging the mountain for ore.

?: (<< dwarf~2 be [there in home inside around] >>) No, they've gone to work.

?: (when * dwarf~2) They will come back at dusk.

?: (<<[how like] dwarf~2 live>>) It's a bit strange living with seven men half my size, but you get used to it.

?: (<<be dwarf~2 [friendly nice pleasant affable amiable attentive helpful loving sympathetic kind tender fond]>>) Yes, they're great.
	$trust += 1

u: ( <<  [dwarf~2] [food~1 apple] [like enjoy eat] >>)
	I guess the dwarves might like apples but they won't eat anything coming from a stranger.
	a: (why) They're a bit paranoid.

?: (<< I wait  >>) You can wait outside but they've forbidden me to let anyone in.
	a:(why) ^reuse(justify_identity.TELLMEWHY)

?:(<< who live house >>) Oh, this is the house of the seven dwarves.
	$dwarves = 1

?:(<< dwarf~2 help you >>) They've offered me food and lodging, and saved me when my stepmother tried to kill me.
	$plot = 1

#! ----------------------------------------------------------------
#! Topic-related Gambits
#! ----------------------------------------------------------------

t: 	[The dwarves really insisted that I not to let anyone in.]
	[The dwarves know that I'm naturally friendly so they made me promise to be wary of strangers.]
	a:([smart good clever]) Yes, they are nobody's fools.
	a:(why) ^reuse(justify_identity.TELLMEWHY)
t: ($dwarves==1 $offered==1) The dwarves would think I'm really stupid if I accept another gift from a stranger.