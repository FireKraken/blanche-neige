topic: ~dwarves ($dwarves dwarf~2 ~mythical_intelligent_being)

#! Possible related pronouns
u: (_they) ^mark(dwarves _0)  ^repeat() ^keep() 
u: (_them) ^mark(dwarves _0)  ^repeat() ^keep() 

u: ($end==1) The door is shut, no one is hearing you. ^keep() ^repeat()

#! ----------------------------------------------------------------
#! Undermine dwarves tactic
#! ----------------------------------------------------------------

s: ([they dwarf~2] * [keep prison~1 ~jail kidnap~1 confine~2]) 
	You think so? ... they seem so nice.
	$trust += 1

s: ([they dwarf~2] * stupid~1)
	$trust += 1
	Yeah, I guess they are kinda dumb. Can you believe that they thought the earth was round?

s: ([they dwarf~2] * ~badness)
	$trust -= 1
	Don't talk like that about these generous creatures!

s: (~acquiring_verbs * ~fruit * [they dwarf~2])
	$trust -= 1
	That seems like an awful lot for a merchant to just give away.

u: ( * [they dwarf~2] * [food~1 apple])
	It's true that I'm tired of bread and cabbages every day.
	$trust += 1

#! ----------------------------------------------------------------
#! Praise dwarves tactic
#! ----------------------------------------------------------------

s: ([they dwarf~2] * ~goodness) Yes they've been very good to me
	$trust += 1

#! ----------------------------------------------------------------
#! Fishing for info
#! ----------------------------------------------------------------

?: (who * be * dwarf~1) I live with seven little men.
	a:(why) They've been very nice to me.

?: (where * be * dwarf~1) Digging the mountain for ore.

?: (when * dwarf~1) They will come back at dusk.

#! ----------------------------------------------------------------
#! Topic-related Gambits
#! ----------------------------------------------------------------

t: The dwarves told me not to let anyone in.
t: ($dwarves==1) The dwarves would think I'm really stupid if I accept another gift from a stranger.