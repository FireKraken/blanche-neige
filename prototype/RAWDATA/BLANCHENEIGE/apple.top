concept: ~delicious (delicious delectable savory yummy tasty)
concept: ~samaritan (samaritan do-gooder humanitarian johny-on-the-spot helpfulness~2)

topic: ~sell_apple (apple ~fruit buy sell food hunger hungry snack meal give ~delicious it)

#! Possible related pronouns
u: (_it) ^mark(apple _0)  ^repeat() ^keep() 
u: (_them) ^mark(apple _0)  ^repeat() ^keep() 
u: (_they) ^mark(apple _0)  ^repeat() ^keep() 
u: (_one) ^mark(apple _0)  ^repeat() ^keep() 

u: ($end==1) The door is shut, no one is hearing you. ^keep() ^repeat()

#! ----------------------------------------------------------------
#! Offer apple
#! ----------------------------------------------------------------

	
t: ($trust==3) All right, I'll have a bite of this apple. Arrrhh you vile woman! I'm too young to die! The dwarves will definitely think me an idiot now!

u: ($offered!=1 !they !dwarf~2 [~acquire_imperatives here want~1 ~acquiring_verbs have] * [apple one it] ) 
	$offered = 1
	Who are you to be offering apples to strangers? ^addtopic(~justify_identity)
	
u: ($offered!=1 $peddler!=1 apple) Sorry, what were you saying about those apples?

u: ($trust<3 [~acquire_imperatives here want ~acquiring_verbs ~eat] * [apple ~it_words] ) No thanks, I don't trust you enough.
u: ($trust<3 [~acquire_imperatives here want ~acquiring_verbs ~eat ] * [apple ~it_words] ) I still don't trust you enough to eat that.
u: ($trust<3 [~acquire_imperatives here want ~acquiring_verbs ~eat] * [apple ~it_words] ) Not eating that apple.
u: ($trust<3 [~acquire_imperatives here want ~acquiring_verbs ~eat] * [apple ~it_words] ) Stop waving that apple in my face.
u: ($trust<3 [~acquire_imperatives here want ~acquiring_verbs ~eat] * [apple ~it_words] ) There are other people in this world that could want this apple!
u: ($trust<3 [~acquire_imperatives here want ~acquiring_verbs ~eat] * [apple ~it_words] ) No. ^keep() ^repeat()
u: ($trust==3) All right, I'll have a bite of this apple. Arrrhh you vile woman! I'm too young to die! The dwarves will definitely think me an idiot now!

s:(I * have * too~1 * apple) There's no such thing as too many apples.

#! ----------------------------------------------------------------
#! Selling the apple
#! ----------------------------------------------------------------

u: (<<apple ~delicious>>)
	$trust += 1
	It does look good.

u: (<<apple [cheap bargain "low price" "good price"]>>) Price is not the issue here.
	a:(what) Trust is the issue.

u: (<<apple magical~1>>)
	I've become wary of magic.
	a:(why) My stepmother has been using magic to kill me.
		$plot = 1
		^addtopic(~assassination_plot)

u: (<<apple [wellness~1 healthy "good for you" nutritive "well-being"] >>)
	$trust += 1
	It's true that apples are healthy.

u: (<<apple day doctor>>) I knew you'd say that.

u: (dwarf~1 * like~5 * apple) They do, but they're very picky when it comes to the origin of what they eat.

#! ----------------------------------------------------------------
#! Fishing for info
#! ----------------------------------------------------------------

?: (why * [accept~5 ~taste try~1 ~eat]) 
	$plot = 1
	The last two times a strange woman came to my door to give me something, it turned out to be assassination attempts from my stepmother. 

	#! If identity clear, matter of trust

#! ----------------------------------------------------------------
#! Topic-related gambits
#! ----------------------------------------------------------------

t: I can't accept anything from you.
	a:(why) Because you're a stranger.