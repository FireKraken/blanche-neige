concept: ~delicious (delicious delectable savory yummy tasty)
concept: ~samaritan (samaritan do-gooder humanitarian johnny-on-the-spot helpfulness~2)
concept: ~takethis ("take this" "would you like" "how about" "would you care for" "would you care to join me")

topic: ~sell_apple (apple ~fruit buy sell food hunger hungry snack meal give ~delicious it eat gift present)

#! Possible related pronouns
u: (_it) ^mark(apple _0)    ^repeat() ^keep()
u: (_them) ^mark(apple _0)  ^repeat() ^keep() 
u: (_they) ^mark(apple _0)  ^repeat() ^keep() 
u: (_one) ^mark(apple _0)  ^repeat() ^keep() 
u: (_this) ^mark(apple _0)  ^repeat() ^keep() 
u: (_fruit) ^mark(apple _0)  ^repeat() ^keep() 


u: ($end==1) ^reuse(~catchall.DOORCLOSED)
u: ($end==2 [booya booyah]) ^reuse(~catchall.RESURRECT)
u: ($end==2) ^reuse(~catchall.ISDEAD)

#! ----------------------------------------------------------------
#! "I will have one too tactic"
#! ----------------------------------------------------------------

u:(!you << I [eat taste try bite] apple >>) CTplus That is somewhat convincing but you could have poisoned only parts of it!
	$trust += 1

u:(<< i eat apple whole >>) That's easy: you eat the good apple and give me the poisoned one.

u:($trust<3 <<[share split half] apple>>) CTplus That's very generous but I'm not sure that I trust you enough yet.
	$trust += 1

#! ----------------------------------------------------------------
#! Bribe tactic
#! ----------------------------------------------------------------

u:([I here take have give] * [~currency dollars gold silver coins money cash]) CTminus You're giving me money to taste your apple? This is getting really suspicious.
	$trust -= 1

?:(<<help [for against pay] apple>>) I'm not looking for a reward, it's just the circumstances. ^poptopic() ^addtopic(~assassination_plot)

#! ----------------------------------------------------------------
#! Threatening
#! ----------------------------------------------------------------

u: (<<[~acquire_imperatives here taste buy want~1 ~acquiring_verbs have] [~insults unpleasant_person~1] apple>>) You're not being very amiable.
u: (<<[~acquire_imperatives here taste buy want~1 ~acquiring_verbs have] [~insults unpleasant_person~1] apple>>) CFail I don't have to listen to this, bye.
	$end = 1

#! ----------------------------------------------------------------
#! Selling the apple
#! ----------------------------------------------------------------

u:  (<<apple [~delicious tasty "sweet"]>>)
	$trust += 1
	CTplus It does look delicious.

u: (<<apple [cheap bargain "low price" "good price" free gratis]>>) Price is not the issue here.
	a:(what) Trust is the issue.

u: (<<apple magical~1>>)
	I've become wary of magic.
	a:(why) My stepmother has been using magic to kill me.
		$plot = 1
		^poptopic() ^addtopic(~assassination_plot)

u: (<<apple not [poisoned poison]>>) I may be naive, but I'll need more than your word.

u: (!not <<apple [poison poisoned]>>) CTminus I appreciate your candour but I don't think I'll be eating this apple now.
	$trust -= 2

u: (<<apple [wellness~1 healthy good "good for you" nutritive "well-being" "fat" organic] >>)
	$trust += 1
	CTplus It's true that apples are healthy.

u: (<<apple day doctor>>) I knew you'd say that.

u: (dwarf~1 * like~5 * apple) They do, but they're very picky when it comes to the origin of what they eat.
	$dwarves = 1

u: (<< you apple pie >>) The dwarves would love this but they won't eat anything they haven't grown themselves.
	$dwarves = 1

u: (<< apple you [plain ugly "less beautiful" hideous "bad looking"] >>) That's interesting, but I'm not very fond of magic.
	$trust += 1
	a:(why) CTplus My stepmother has tried using magic to kill me.
		$plot = 1
		^poptopic() ^addtopic(~assassination_plot)

u: (<<~takethis ~food>>) You mean one of those apples?
	a:(~Yes)
		^input(take this apple)
	a:(~No) In any case, I'm not hungry, thanks.

u: (<< apple make [feel life] better >>) CTplus It's true that fruits do that.
	$trust += 1

#! ----------------------------------------------------------------
#! Apple is Magical (in a good way)
#! ----------------------------------------------------------------

s: (<<apple [protect defend help]>>) Wait, this apple can protect me from my stepmother?
	a:([~Yes protect defend help maybe perhaps] ) CTplus I'd like to believe that.
		$trust += 1
	a:(~No) Too bad.

s:(<<apple [protect defend help]>>) Yes, you've said that. It's a tempting thought but you could be lying.

#! ----------------------------------------------------------------
#! Fishing for info
#! ----------------------------------------------------------------

?: (why * [accept~5 ~taste try~1 ~eat]) 
	$plot = 1
	The last two times a strange woman came to my door to give me something, it turned out to be assassination attempts from my stepmother. 

?: (<< you like apple >>) Of course.	

?: (<< you like ~fruit >>) Sure, everyone likes fruits.

u: (<< [why would] [i one someone somebody] poison [apple food] >>) Because you could be my stepmother who wants to kill me disguised as an innocent old woman.



#! ----------------------------------------------------------------
#! Offer apple
#! ----------------------------------------------------------------

s:(<< [gift present] you >>) You're offering me that apple?
	a:(~yes) That's sweet. However I can't accept anything from strangers.
		b: (why)^reuse(justify_identity.TELLMEWHY)
	a:(~no) What is it then?
		b: (_~noun) Oh, but I really don't need '_0.

s: (<< leave apple later >>) Keep it for someone who will eat it.	

s: (<< you not should eat apple >>) Thank you for the warning.

#! t: ($trust>=3) CWin All right, I'll have a bite of this apple. Arrrhh you vile woman! I'm too young to die! The dwarves will definitely think me an idiot now! ^addtopic(~catchall)
#! 	$end = 2

u: ($identity!=1 $offered!=1 !they !dwarf~2 [share ~acquire_imperatives here taste buy want~1 ~acquiring_verbs have] * [apple one it] ) 
	$offered = 1
	Excuse me, but who are you to be offering apples to strangers? ^poptopic() ^addtopic(~justify_identity) 
		a: (_*) ^input(I am '_0) ^fail(sentence)  
	
#! u: ($offered!=1 $peddler!=1 apple) Sorry, are you here to sell apples?
#!	a:(~yes) That's nice but I can't accept anything from strangers.
#!		$purpose = 1
#!		$peddler = 1
#!		b:(why) ^reuse(justify_identity.TELLMEWHY)
#!	a:(!give ~no) Then what are you doing here?
#!	a:(give) That's nice but I really can't accept anything in the circumstances.
#!		b:(why) ^reuse(justify_identity.TELLMEWHY)

u: ($trust<3 !~insults !unpleasant_person~1 [bite ~acquire_imperatives here taste buy want ~acquiring_verbs ~eat] * [apple ~it_words] ) No thanks, I don't trust you enough.
u: ($trust<3 !~insults !unpleasant_person~1 [bite ~acquire_imperatives here taste buy want ~acquiring_verbs ~eat] * [apple ~it_words] ) I still don't trust you enough to eat that.
u: ($trust<3 !~insults !unpleasant_person~1 [bite ~acquire_imperatives here taste buy want ~acquiring_verbs ~eat] * [apple ~it_words] ) Not eating that apple.
u: ($trust<3 !~insults !unpleasant_person~1 [bite ~acquire_imperatives here taste buy want ~acquiring_verbs ~eat] * [apple ~it_words] ) Stop waving that apple in my face.
u: ($trust<3 !~insults !unpleasant_person~1 [bite ~acquire_imperatives here taste buy want ~acquiring_verbs ~eat] * [apple ~it_words] ) There are other people in this world that could want this apple!
u: ($trust<3 !~insults !unpleasant_person~1 [bite ~acquire_imperatives here taste buy want ~acquiring_verbs ~eat] * [apple ~it_words] ) No. ^keep() ^repeat()
u: ($trust>=3) CWin All right, I'll have a bite of this apple. Arrrhh you vile woman! I'm too young to die! The dwarves will definitely think me an idiot now! ^poptopic() ^addtopic(~catchall)
	$end = 2

s:(I * have * too~1 * apple) There's no such thing as too many apples.

#! ----------------------------------------------------------------
#! Topic-related gambits
#! ----------------------------------------------------------------

r:($offered!=1) CGambit That apple you're holding, is that your lunch? 
	a: (~yes) Lucky you!
	a: (~no) Is it the reason of your visit?
		b: (%length<2 ~yes) So what about the apple, then?
		b: (~no) Oh, ok.
	

r:($offered!=1) CGambit That's a nice apple you have there.
	a: ([thank thanks~2 ~emothanks]) You're welcome.

t: ($offered==1) 	CGambit[I'm sorry to be like that but I just can't accept anything from anyone.]
					CGambit[That apple seems nice, but I wouldn't risk my life for a generic fruit.]
					CGambit[I know I sound impolite but I'm sure you would do the same if you feared to be poisoned.]
	a:([why "how come" explain "tell me"]) ^reuse(justify_identity.TELLMEWHY) 