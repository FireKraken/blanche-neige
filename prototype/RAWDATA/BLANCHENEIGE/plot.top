concept: ~murderplot (~plot plan plan~3 assassination murder scheme machination intrigue conspiracy cabal kill murder)
concept: ~evilqueen (queen "evil queen" stepmother she)

topic: ~assassination_plot (~murderplot ~evilqueen)

#! Possible related pronouns
u: (_she) ^mark(queen _0)  ^repeat() ^keep() 
u: (_her) ^mark(queen _0)  ^repeat() ^keep() 
u: (_it) ^mark(plot _0)  ^repeat() ^keep() 

u: ($end==1) The door is shut, no one is hearing you. ^keep() ^repeat()

#! ----------------------------------------------------------------
#! fishing for more info
#! ----------------------------------------------------------------

?: ([(how * ~murderplot) (what * ~evilqueen * give)]) First it was bodice lace that nearly squeezed the death out of me. Then it was a poisoned comb. Can you imagine such a thing?
	a: (~YES) You read too much gothic fiction.
	a: (~NO) She's out of her mind.

?: ( << why ~evilqueen ~murderplot >>) Apparently she's jealous of my beauty and wants to kill me for that!

?: (who * ~evilqueen) Simply the kingdom's queen!

?: ([what who] * ~murderplot) My stempother wants to kill me so that she can claim to be the most beautiful living woman.

?: ([Damocles sword]) I'm referring to my stepmother's plot to assassinate me.
	$plot = 1

#! ----------------------------------------------------------------
#! Compassion move
#! ----------------------------------------------------------------

s: ( << [that it situation ~evilqueen ~murderplot] [horrible awful tragic sad poor difficult] >> )
	$trust += 1
	I know! This is so unfair! 

s: (poor * [thing you]) Thank you for the compassion.


#! ----------------------------------------------------------------
#! Not being stepmother
#! ----------------------------------------------------------------

s: (~evilqueen * [dead~1 away~3])
	$trust += 1
	Really!? That's great news!

s: (I * not *[look resemble like] ~evilqueen) Yes but she tends to disguise herself.

s: (I * not * ~evilqueen) How do I know that?

s: (I * [hate~2 despise~1] ~evilqueen) She's not very popular.


#! ----------------------------------------------------------------
#! Topic related gambits
#! ----------------------------------------------------------------

t: Look, I can't be too cautious with my stepmother wanting to kill me.
	$plot = 1
t: ($plot==1) It's so hard living with this sword of Damocles over my head.