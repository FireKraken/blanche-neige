concept: ~murderplot (~plot plan plan~3 assassination murder scheme machination intrigue conspiracy cabal kill murder anxiety stress circumstances)
concept: ~evilqueen (queen "evil queen" stepmother she stepmom)
concept: ~agreement ("I know" "I agree" "I understand" "totally" "totes")

topic: ~assassination_plot (~murderplot ~evilqueen save)

#! Possible related pronouns
u: (_she) ^mark(queen _0)  ^repeat() ^keep() 
u: (_her) ^mark(queen _0)  ^repeat() ^keep() 
u: (_it) ^mark(plot _0)  ^repeat() ^keep() 
u: (_that) ^mark(plot _0)  ^repeat() ^keep() 

u: ($end==1) ^reuse(~catchall.DOORCLOSED)
u: ($end==2 [booya booyah]) ^reuse(~catchall.RESURRECT)
u: ($end==2) ^reuse(~catchall.ISDEAD)

#! ----------------------------------------------------------------
#! fishing for more info
#! ----------------------------------------------------------------

?: HOWKILL ($stepmother!=1 [(how * ~murderplot) (what * ~evilqueen * give)]) First it was bodice lace that nearly squeezed the death out of me. Then it was a poisoned comb. Can you imagine such a thing?
	a: (~YES) You read too much gothic fiction.
	a: ([~NO ~badness]) She's out of her mind.

?: ($stepmother!=1 << why [~evilqueen they stranger] ~murderplot >>) Apparently my stepmother's jealous of my beauty and wants to kill me for that!
	a:([silly ridiculous ludicrous preposterous bizarre absurd preposterous unbelievable grotesque farcical]) Isn't it?!
		$trust+=1

?: ($stepmother!=1 who * ~evilqueen) Simply the kingdom's queen!

?: ($stepmother!=1 [what who] * ~murderplot) My stepmother wants to kill me so that she can claim to be the most beautiful living woman.

?: ($stepmother!=1 ~evilqueen * ~murderplot * you) Yes, she's evil and is jealous of my beauty!

?: ($stepmother!=1 stress~3) I have to sleep with one eye open since my stepmother keeps trying to kill me!
	$plot = 1

u: ([how ~speak_verbs] ~murderplot) ^reuse(HOWKILL)


#! ----------------------------------------------------------------
#! Offer to solve problem
#! ----------------------------------------------------------------

u: (<< I can help >>) That would be great, but what can you do?

u: (<< I here save you >>) That would be great, but what can you do?

u: (<< I [~damage_verbs hunt capture punish kidnap spell dissuade] ~evilqueen >>)You would really do that?! Thanks! CTplus 
	$trust += 1

u: (<< you not [hide~5 flee escape avoid] [always forever eternally] >>) I know, but I don't know what to do.CTplus
	$trust += 1

u: (<< you [why should ought could] ~damage_verbs ~evilqueen >>) I'm just a frail young girl. I was raised to believe that this was a job for a prince.

#! ----------------------------------------------------------------
#! Compassion move
#! ----------------------------------------------------------------

s: ( << [that it situation ~evilqueen ~murderplot] [horrible awful tragic sad poor difficult unfortunate] >> )
	$trust += 1
	I know! This is so unfair! CTplus
	a:([yes unfair indeed]) You are nice.


s: (poor * [thing you]) Thank you for the compassion.

s:($plot==1 <<[I my] [sympathy sympathize sorry understand "feel you"] >>) That is nice of you.
	$trust += 1

s:(<< ~evilqueen happy you [should would] >>) I know.

s:([relax chill calm]) Easy to say when no one is trying to kill you!

#! ----------------------------------------------------------------
#! Truce tactic
#! ----------------------------------------------------------------

s: ($stepmother==1 I * want * [friendly~2 chat~3]) Somehow I doubt that, given our last three meetings. Whatâ€™s your plan this time?
	threatened += 1 

#! ----------------------------------------------------------------
#! Not being stepmother
#! ----------------------------------------------------------------

s: (<<~evilqueen  [want try]  [kill capture hurt ~damage_verbs]  me >>) Oh! Poor thing! How I understand you! It's not for me to invite you in the dwarves' house but you can go hide in this shed.CTplus
	$trust += 1
	$purpose = 1

u: (<< ~evilqueen [information info news scoop rumor] >>) Tell me more!

s:(<< ~evilqueen [woods wood here around close] >>) Arg! I really should hide!

s: ($stepmother!=1 ~evilqueen * [dead~1 away~3])
	$trust += 1
	Really!? That's great news!CTplus

s: ($stepmother!=1 <<I  [not nothing] [look resemble like] ~evilqueen>>) Yes but she tends to disguise herself.

s: ($stepmother!=1 I * not * ~evilqueen) How do I know that?

s: ($stepmother!=1 I * [hate~2 despise~1] * ~evilqueen) She's not very popular.
	$trust += 1

s:(<<I not [look resemble resemblance] ~evilqueen>>) True, but she's usually disguised.

s:(<<I not [costume disguise] >>) That easy to say. My stepmother's a magician and can completely change her appearance.


#! ----------------------------------------------------------------
#! Topic related gambits
#! ----------------------------------------------------------------

t: 	[I really wish my stepmother wasn't trying to kill me]
	[It will be hard for me to trust anyone when I know my stepmother could be disguised to kill me.]
	$plot = 1
	a:(I * too) I see you understand.
		$trust += 1
t:  I can't think straight with all this stress.