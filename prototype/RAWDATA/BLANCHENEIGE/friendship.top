concept: ~bro (bro homie ~role_ally "best friend" friend "I like you" "home slice" "dawg" "home boy")
concept: ~sing (sing~3 sing~4 song)
concept: ~thewoods (forest~1 forest~2 flora~2 ~scenery_land landscape)

topic: ~build_friendship (~friend ~befriend ~feeling_alone ~befriend lonely~1 lonely~4 ~sing nature~4 animal~1 ~thewoods)


#! Possible related pronouns 

u: ($end==1) ^reuse(~catchall.DOORCLOSED)
u: ($end==2 [booya booyah]) ^reuse(~catchall.RESURRECT)
u: ($end==2) ^reuse(~catchall.ISDEAD)

#! ----------------------------------------------------------------
#! Loneliness
#! ----------------------------------------------------------------

s: (<<you [lonely~1 lonely~4 ~feeling_alone]>>) Yeah, it sucks being the only human around here.
	a: ($friend!=1 <<I be !~no ~bro>>) CTplus That would be nice.
		$trust += 1 
		$friend = 1

#! ----------------------------------------------------------------
#! Friend activities
#! ----------------------------------------------------------------

u: (!no !not ~sing) CTplus Okay! With a smile and a song, Life is just a sunny day...
	$trust += 1
	$nature = 1

#! ----------------------------------------------------------------
#! Cement friendship
#! ----------------------------------------------------------------

?: (what * do * trust) CTminus To be honest, your desperate attempts to earn my trust are a bit suspicious.
	$trust -= 1

s: ($trust<2 $friend!=1 I * be * ~bro) I just met you.

s: ($trust>=2 $friend!=1 I * [no not] * stranger~2) CTplus You're right, I have gotten to know you a bit better.
	$trust += 1
	$friend = 1

s: ($trust<2 $friend!=1 I * [no not] * stranger~2) Uh, yes you are.

s: ($trust>=2 $friend!=1 I * be * !~no * ~bro) CTplus I guess I do know you a bit better now.
	$trust += 1
	$friend = 1

s:(trust<2 !no !not I * ~like * talking~1 * you) CTplus Uh, thanks?
	$trust += 1

s:(trust>=2 !no !not I * ~like * talking~1 * you) CTplus Thanks, I'm enjoying this too!
	$trust += 1

s:($trust<2 $friend!=1 !no !not I * ~like * you) That's nice...

s:($trust>=2 $friend!=1 !no !not  I * ~like * you) CTplus Aw, thanks!
	$trust += 1

#! ----------------------------------------------------------------
#! Love of the woods tactics?
#! ----------------------------------------------------------------

s: (<<animal~1 ~positivevalue>>) CTplus Sometimes I sing with them!
	$trust += 1
	$nature = 1

s: (<<~thewoods  ~positivevalue>>) CTplus Yes, the trees are beautiful, aren't they?
	$trust += 1
	$nature = 1

#! ---------------------------------------------------------------
#! Length based tactics
#! ---------------------------------------------------------------
u: (%input>6 $friend=1 [talking speak discuss converse] *be *[fun pleasure] ) Yes, but I still don’t feel comfortable talking to you.
	$friend = 1
u: (%input>10 $friend=1 [talking speak discuss converse] * [while time] ) CTplus It’s been nice getting to know you but I can’t stay much longer. I’m fairly busy.
	$friend = 2
	$trust += 1
#! -----(%input>15 $friend=2) [don’t please] * [leave stay go] Well, I guess I can stay a little longer.